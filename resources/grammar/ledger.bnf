(* ledger grammar file *)
(* vim: set ft=ebnf : *)

ledger = (
      NL
    | CommentHeader
    | CommentBlock
    | IncludeFile
    | AccountDefinition
    | CommodityDefinition
    | CommodityConversion
    | CommodityPrice
)+;


(* Text *)
<NL> = <'\n'> ;
<Indent> = <'    '> ;
<Text> = #"\S?[^\n]*\S" ;

(* Dates *)
Date = #"\d{4}-\d\d-\d\d"

(* Numbers *)
<NonZeroDigit> = '1' | '2' | '3' | '4' | '5' | '6' | '7' | '8' | '9' ;
<Digit>        = '0' | NonZeroDigit ;
Number         = ['-'] , ( Digit | ( NonZeroDigit , ( Digit | ',' )+ ) ) , [ '.' , Digit+ ] ;

(* Comments & Includes *)
CommentHeader = <';;;;; '> , #"[^;]*[^ ;]" , <' ;;;;;\n'> ;
CommentBlock = ( <'; '> , Text , NL | <';'> , '\n' )+ , !CommentBlock ;
IncludeFile = <'include '> , #"\S+" , NL ;

(* Account Definitions *)
<AccountPathWord> = #"[A-Z0-9][a-zA-Z0-9-]+" ;
AccountPathSegment = AccountPathWord , ( ' ' , AccountPathWord )* ;
AccountPath = AccountPathSegment , ( <':'> , AccountPathSegment )* ;
AccountAlias = #"[a-z][a-zA-Z0-9-]*[a-z0-9]" ;
AccountName = AccountPath | AccountAlias ;

AccountDefinition = <'account '> , AccountPath , NL , AccountDirective* ;
<AccountDirective> = Indent , ( AccountAliasDirective | AccountNote | AccountAssertion ) , NL ;
AccountAliasDirective = <'alias '> , AccountAlias ;
AccountNote = <'note '> , Text ;
AccountAssertion = <'assert '> , Text ;

(* Commodity Definitions *)
CommodityCode = '$' | #"[a-zA-Z][a-zA-Z0-9_]+" ;
Quantity = ( CommodityCode , <[' ']> , Number ) | ( Number , <[' ']> , CommodityCode ) ;

CommodityDefinition = <'commodity '> , CommodityCode , NL , CommodityDirective* ;
<CommodityDirective> = Indent , ( CommodityNote | CommodityFormat | CommodityOption ) , NL ;
CommodityNote = <'note '> , Text ;
CommodityFormat = <'format '> , Text ;
CommodityOption = 'nomarket' | 'default' ;

(* Commodity Conversions and Prices *)
CommodityConversion = <'C '> , Quantity , <' = '> , Quantity , NL ;
CommodityPrice = <'P '> , Date , <' '> , CommodityCode , <' '+> , Quantity , NL ;
