(* ledger grammar file *)
(* vim: set ft=ebnf : *)

<LedgerEntry> = (
      CommentHeader
    | CommentBlock
    | IncludeFile
    | AccountDefinition
    | CommodityDefinition
    | CommodityConversion
    | CommodityPrice
    | Transaction
)+;


(* Text *)
<NL> = <'\n'> ;
<Indent> = <'    '> ;
<Text> = #"\S?[^\n]*\S" ;
<TagName> = #"[a-zA-Z0-9-]+" ;

(* Dates *)
Date = #"\d{4}-\d\d-\d\d" ;

(* Numbers *)
<NonZeroDigit> = '1' | '2' | '3' | '4' | '5' | '6' | '7' | '8' | '9' ;
<Digit>        = '0' | NonZeroDigit ;
Number         = ['-'] , ( Digit | ( NonZeroDigit , ( Digit | <','> )+ ) ) , [ '.' , Digit+ ] ;
Percentage     = Number , <'%'> ;

(* Comments & Includes *)
CommentHeader = <';;;;; '> , #"[^;]*[^ ;]" , <' ;;;;;\n'> ;
CommentBlock = ( <'; '> , Text , NL | <';'> , '\n' )+ , !CommentBlock , !Transaction ;
IncludeFile = <'include '> , #"\S+" , NL ;

(* Account Definitions *)
<AccountPathWord> = #"[A-Z0-9&][a-zA-Z0-9-.]*" ;
AccountPathSegment = AccountPathWord , ( ' ' , AccountPathWord )* ;
AccountPath = AccountPathSegment , ( <':'> , AccountPathSegment )* ;
AccountAlias = #"[a-z][a-zA-Z0-9-]*[a-z0-9]" ;
<AccountRef> = AccountPath | AccountAlias ;

AccountDefinition = <'account '> , AccountPath , NL , AccountDirective* ;
<AccountDirective> = Indent , ( AccountAliasDirective | AccountNote | AccountAssertion ) , NL ;
AccountAliasDirective = <'alias '> , AccountAlias ;
AccountNote = <'note '> , Text ;
AccountAssertion = <'assert '> , Text ;

(* Commodity Definitions *)
CommodityCode = '$' | #"[a-zA-Z][a-zA-Z_]*" | <'"'> , #"[a-zA-Z][a-zA-Z0-9_]*" , <'"'> ;
Quantity = '0' | ( CommodityCode , <[' ']> , Number ) | ( Number , <[' ']> , CommodityCode ) ;

CommodityDefinition = <'commodity '> , CommodityCode , NL , CommodityDirective* ;
<CommodityDirective> = Indent , ( CommodityNote | CommodityFormat | CommodityOption ) , NL ;
CommodityNote = <'note '> , Text ;
CommodityFormat = <'format '> , Text ;
CommodityOption = 'nomarket' | 'default' ;

(* Commodity Conversions and Prices *)
CommodityConversion = <'C '> , Quantity , <' = '> , Quantity , NL ;
CommodityPrice = <'P '> , Date , <' '> , CommodityCode , <' '+> , Quantity , NL ;

(* Transactions *)
Transaction = TxSource* , Date , [ <' '> , TxStatus ] , <' '> , !TxStatus , TxMemo , NL , TxDetail* , Posting+ ;
TxSource = <'; '> , AccountAlias , <'|'> , Text , NL ;
TxStatus = '!' | '*' ;
TxMemo = Text ;
<TxDetail> = Indent , <'; '> , ( TxMeta | TxComment ) , NL ;
TxMeta = TagName , <': '> , Text | <':'> , TagName , <':'> ;
TxComment = !TxMeta , Text ;

(* Postings *)
Posting = Indent ,
          PostingAccount ,
          [ <'  '> ,
            [ <' '*> , PostingAmount ,
              [ <' '+> , PostingLotCost ,
                [ <' '+> , PostingLotDate ] ] ,
              [ <' '+> , PostingPrice ] ] ,
            [ <' '*> , PostingBalance ] ,
            [ <' '*> , <'  ; '> , ( PostingMeta | PostingComment ) ] ] ,
          NL ,
          PostingDetail* ;

<PostingAccount> = PostingRealAccount | PostingSoftVirtualAccount | PostingHardVirtualAccount ;
PostingRealAccount = AccountRef ;
PostingSoftVirtualAccount = <'('> , AccountRef , <')'> ;
PostingHardVirtualAccount = <'['> , AccountRef , <']'> ;
PostingAmount = Quantity ;
PostingLotCost = <'{'> , Quantity , <'}'> ;
PostingLotDate = <'['> , Date , <']'> ;
PostingPrice = <'@'> , <' '+> , Quantity ;
PostingBalance = <'='> , <' '+> , Quantity ;
<PostingDetail> = Indent , Indent , <'; '> , ( PostingDate | PostingMeta | LineItem | PostingComment ) , NL ;
PostingDate = <'['> , <['=']> , Date , <']'> ;
PostingMeta = !LineItemTag , TagName , <': '> , Text | <':'> , TagName , <':'> ;
PostingComment = !LineItemTag , !PostingDate , !PostingMeta , Text ;

LineItem = <LineItemTag> , #"\S+( \S+)*" , [ <' '+> , LineItemAmount , [ <' '+> , LineItemDetail ] , [ <' '+> , LineItemTaxGroup ] ] ;
LineItemTag = 'item: ' ;
LineItemAmount = Quantity ;
<LineItemDetail> = <'('> , <' '*> , LineItemQuantity , <' '+> , <'@'> , <' '+> , LineItemCost , <')'> ;
LineItemQuantity = Number | Quantity ;
LineItemCost = Quantity | Percentage ;
LineItemTaxGroup = #"\*+" ;
